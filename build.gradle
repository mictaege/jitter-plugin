group 'com.github.mictaege'
version '2023.1-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

wrapper {
	gradleVersion = "6.7"
	distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile gradleApi()
    compile 'com.google.guava:guava:23.0'
    compile 'org.codehaus.groovy:groovy-all:2.5.8'
    compile 'com.github.mictaege:spoon-gradle-plugin:2023.1-SNAPSHOT'
    compile 'com.github.mictaege:jitter-api:2023.1-SNAPSHOT'
    compile 'org.eclipse.jdt:org.eclipse.jdt.core:3.33.0'
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.2.11"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") } //sign only when upload archives
    sign configurations.archives
}

//don't refer to username and pwd directly so it could be executed in another environment with another _gradle.properties_
String publishUsername = hasProperty("ossrhUsername") ? property("ossrhUsername") : ""
String publishPassword = hasProperty("ossrhPassword") ? property("ossrhPassword") : ""

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: publishUsername, password: publishPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: publishUsername, password: publishPassword)
            }

            pom.project {
                name 'jitter-plugin'
                packaging 'jar'
                description 'The jitter-plugin is a Gradle plugin to build and distribute different flavours of an application from a single source base.'
                url 'https://github.com/mictaege/jitter-plugin'
                scm {
                    connection 'https://github.com/mictaege/jitter-plugin.git'
                    url 'https://github.com/mictaege/jitter-plugin'
                }
                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id 'mictaege'
                        name 'Michael Taege'
                    }
                }
            }
        }
    }
}


